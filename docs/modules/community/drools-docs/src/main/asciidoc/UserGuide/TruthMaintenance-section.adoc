= Truth Maintenance with Logical Objects
:imagesdir: ..

== Overview

After regular inserts you have to retract facts explicitly.
With _logical_ assertions, the fact that was asserted will be automatically retracted when the conditions that asserted it in the first place are no longer true.
Actually, it's even cleverer then that, because it will be retracted only if there isn't any single condition that supports the logical assertion.

Normal insertions are said to be __stated__, i.e., just like the intuitive meaning of "stating a fact" implies.
Using a `HashMap` and a counter, we track how many times a particular equality is __stated__; this means we count how many different instances are equal.

When we _logically_ insert an object during a RHS execution we are said to _justify_ it, and it is considered to be justified by the firing rule.
For each logical insertion there can only be one equal object, and each subsequent equal logical insertion increases the justification counter for this logical assertion.
A justification is removed by the LHS of the creating rule becoming untrue, and the counter is decreased accordingly.
As soon as we have no more justifications the logical object is automatically retracted.

If we try to _logically_ insert an object when there is an equal _stated_ object, this will fail and return null.
If we _state_ an object that has an existing equal object that is _justified_ we override the Fact; how this override works depends on the configuration setting ``WM_BEHAVIOR_PRESERVE``.
When the property is set to discard we use the existing handle and replace the existing instance with the new Object, which is the default behavior; otherwise we override it to _stated_ but we create an new ``FactHandle``.

This can be confusing on a first read, so hopefully the flow charts below help.
When it says that it returns a new ``FactHandle``, this also indicates the `Object` was propagated through the network.

.Stated Insertion
image::HybridReasoning/Stated_Assertion.png[align="center"]


.Logical Insertion
image::HybridReasoning/Logical_Assertion.png[align="center"]


=== Bus Pass Example With Inference and TMS


The previous example was issuing ID cards to over 18s, in this example we now issue bus passes, either a child or adult pass.

[source]
----
rule "Issue Child Bus Pass" when
    $p : Person( age < 16 )
then
    insert(new ChildBusPass( $p ) );
end
 
rule "Issue Adult Bus Pass" when
    $p : Person( age >= 16 )
then
    insert(new AdultBusPass( $p ) );
end
----


As before the above example is considered monolithic, leaky and providing poor separation of concerns.

As before we can provide a more robust application with a separation of concerns using inference.
Notice this time we don't just insert the inferred object, we use "insertLogical":

[source]
----
rule "Infer Child" when
    $p : Person( age < 16 )
then
    insertLogical( new IsChild( $p ) )
end
rule "Infer Adult" when
    $p : Person( age >= 16 )
then
    insertLogical( new IsAdult( $p ) )
end
----


A "insertLogical" is part of the Drools Truth Maintenance System (TMS). When a fact is logically inserted, this fact is dependant on the truth of the "when" clause.
It means that when the rule becomes false the fact is automatically retracted.
This works particularly well as the two rules are mutually exclusive.
So in the above rules if the person is under 16 it inserts an IsChild fact, once the person is 16 or over the IsChild fact is automatically retracted and the IsAdult fact inserted.

Returning to the code to issue bus passes, these two rules can + logically insert the ChildBusPass and AdultBusPass facts, as the TMS + supports chaining of logical insertions for a cascading set of retracts.

[source]
----
rule "Issue Child Bus Pass" when
    $p : Person( )
         IsChild( person == $p )
then
    insertLogical(new ChildBusPass( $p ) );
end
 
rule "Issue Adult Bus Pass" when
    $p : Person( age >= 16 )
         IsAdult( person =$p )
then
    insertLogical(new AdultBusPass( $p ) );
end
----


Now when a person changes from being 15 to 16, not only is the IsChild fact automatically retracted, so is the person's ChildBusPass fact.
For bonus points we can combine this with the 'not' conditional element to handle notifications, in this situation, a request for the returning of the pass.
So when the TMS automatically retracts the ChildBusPass object, this rule triggers and sends a request to the person:

[source]
----
rule "Return ChildBusPass Request "when
    $p : Person( )
         not( ChildBusPass( person == $p ) )
then
    requestChildBusPass( $p );
end
----

=== Important note: Equality for Java objects


It is important to note that for Truth Maintenance (and logical assertions) to work at all, your Fact objects (which may be JavaBeans) must override equals and hashCode methods (from java.lang.Object) correctly.
As the truth maintenance system needs to know when two different physical objects are equal in value, _both_ equals and hashCode must be overridden correctly, as per the Java standard.

Two objects are equal if and only if their equals methods return true for each other and if their hashCode methods return the same values.
See the Java API for more details (but do keep in mind you _MUST_ override both equals and hashCode).

TMS behaviour is not affected by theruntime configuration of Identity vs Equality, TMS is always equality.

=== Deleting stated or logically asserted facts from the working memory


By default when a fact is deleted from the working memory Drools attempts to remove it both from the set of stated facts and also from the Truth Maintenance System in case it has been logically asserted.
However, using an overload of the delete method, it is also possible to remove it only from one of the 2.
For instance invoking:

[source]
----
ksession.delete( factHandle, FactHandle.State.LOGICAL );
----


the fact is removed only if it has been logically asserted, but not if it is a stated fact.
In this case, if the fact has been stated its deletion fails silently and it is ignored.

ifdef::backend-docbook[]
[index]
== Index
// Generated automatically by the DocBook toolchain.
endif::backend-docbook[]
