[[_querysection]]
= Query

.query
image::LanguageReference/query.png[align="center"]


A query is a simple way to search the working memory for facts that match the stated conditions.
Therefore, it contains only the structure of the LHS of a rule, so that you specify neither "when" nor "then". A query has an optional set of parameters, each of which can be optionally typed.
If the type is not given, the type Object is assumed.
The engine will attempt to coerce the values as needed.
Query names are global to the KieBase; so do not add queries of the same name to different packages for the same RuleBase.

To return the results use ``ksession.getQueryResults("name")``, where "name" is the query's name.
This returns a list of query results, which allow you to retrieve the objects that matched the query.

The first example presents a simple query for all the people over the age of 30.
The second one, using parameters, combines the age limit with a location.

.Query People over the age of 30
====
[source]
----
query "people over the age of 30" 
    person : Person( age > 30 )
end
----
====

.Query People over the age of x, and who live in y
====
[source]
----
query "people over the age of x"  (int x, String y)
    person : Person( age > x, location == y )
end
----
====


We iterate over the returned QueryResults using a standard "for" loop.
Each element is a QueryResultsRow which we can use to access each of the columns in the tuple.
These columns can be accessed by bound declaration name or index position.

.Query People over the age of 30
====
[source,java]
----
QueryResults results = ksession.getQueryResults( "people over the age of 30" );
System.out.println( "we have " + results.size() + " people over the age  of 30" );

System.out.println( "These people are are over 30:" );

for ( QueryResultsRow row : results ) {
    Person person = ( Person ) row.get( "person" );
    System.out.println( person.getName() + "\n" );
}
----
====


Support for positional syntax has been added for more compact code.
By default the declared type order in the type declaration matches the argument position.
But it possible to override these using the @position annotation.
This allows patterns to be used with positional arguments, instead of the more verbose named arguments.

[source]
----
declare Cheese
    name : String @position(1)
    shop : String @position(2)
    price : int @position(0)
end
----


The @Position annotation, in the org.drools.definition.type package, can be used to annotate original pojos on the classpath.
Currently only fields on classes can be annotated.
Inheritance of classes is supported, but not interfaces or methods.
The isContainedIn query below demonstrates the use of positional arguments in a pattern; `Location(x, y;)` instead of `Location( thing == x, location == y).`

Queries can now call other queries, this combined with optional query arguments provides derivation query style backward chaining.
Positional and named syntax is supported for arguments.
It is also possible to mix both positional and named, but positional must come first, separated by a semi colon.
Literal expressions can be passed as query arguments, but at this stage you cannot mix expressions with variables.
Here is an example of a query that calls another query.
Note that 'z' here will always be an 'out' variable.
The '?' symbol means the query is pull only, once the results are returned you will not receive further results as the underlying data changes.

[source]
----
declare Location
    thing : String 
    location : String 
end

query isContainedIn( String x, String y ) 
    Location(x, y;)
    or 
    ( Location(z, y;) and ?isContainedIn(x, z;) )
end
----


As previously mentioned you can use live "open" queries to reactively receive changes over time from the query results, as the underlying data it queries against changes.
Notice the "look" rule calls the query without using '?'.

[source]
----
query isContainedIn( String x, String y ) 
    Location(x, y;)
    or 
    ( Location(z, y;) and isContainedIn(x, z;) )
end

rule look when 
    Person( $l : likes ) 
    isContainedIn( $l, 'office'; )
then
   insertLogical( $l 'is in the office' );
end
----


Drools supports unification for derivation queries, in short this means that arguments are optional.
It is possible to call queries from Java leaving arguments unspecified using the static field org.drools.core.runtime.rule.Variable.v - note you must use 'v' and not an alternative instance of Variable.
These are referred to as 'out' arguments.
Note that the query itself does not declare at compile time whether an argument is in or an out, this can be defined purely at runtime on each use.
The following example will return all objects contained in the office.

[source]
----
results = ksession.getQueryResults( "isContainedIn", new Object[] {  Variable.v, "office" } );
l = new ArrayList<List<String>>();
for ( QueryResultsRow r : results ) {
    l.add( Arrays.asList( new String[] { (String) r.get( "x" ), (String) r.get( "y" ) } ) );
}
----


The algorithm uses stacks to handle recursion, so the method stack will not blow up.

It is also possible to use as input argument for a query both the field of a fact as in:

[source]
----
query contains(String $s, String $c)
    $s := String( this.contains( $c ) )
end

rule PersonNamesWithA when
    $p : Person()
    contains( $p.name, "a"; )
then
end
----

and more in general any kind of valid expression like in:

[source]
----
query checkLength(String $s, int $l)
    $s := String( length == $l )
end

rule CheckPersonNameLength when
    $i : Integer()
    $p : Person()
    checkLength( $p.name, 1 + $i + $p.age; )
then
end
----


The following is not yet supported:

* List and Map unification
* Expression unification - pred( X, X + 1, X * Y / 7 )
