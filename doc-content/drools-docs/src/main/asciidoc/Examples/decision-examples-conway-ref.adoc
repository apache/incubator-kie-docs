[id='decision-examples-conway-ref_{context}']
= Conway's Game of Life example decisions (ruleflow groups and GUI integration)

The Conway's Game of Life example decision set, based on the famous cellular automaton by John Conway, demonstrates how to use ruleflow groups in rules to control rule execution. The example also demonstrates how to integrate {PRODUCT} rules with a graphical user interface (GUI), in this case a Swing-based implementation of Conway's Game of Life.

The following is an overview of the Conway's Game of Life (Conway) example:

* *Name*: `conway`
* *Main classes*: `org.drools.examples.conway.ConwayRuleFlowGroupRun`, `org.drools.examples.conway.ConwayAgendaGroupRun` (in `src/main/java`)
* *Module*: `droolsjbpm-integration-examples`
* *Type*: Java application
* *Rule files*: `org.drools.examples.conway.*.drl` (in `src/main/resources`)
* *Objective*: Demonstrates ruleflow groups and GUI integration

NOTE: The Conway's Game of Life example is separate from most of the other example decision sets in {PRODUCT} and is located in
ifdef::DROOLS,JBPM,OP[]
the {PRODUCT_DROOLS} and {PRODUCT_JBPM} integration repository in https://github.com/kiegroup/droolsjbpm-integration/archive/master.zip[GitHub].
endif::[]
ifdef::DM,PAM[]
`~/{PRODUCT_FILE}-sources/src/droolsjbpm-integration-$VERSION/droolsjbpm-integration-examples` of the *{PRODUCT_PAM} {PRODUCT_VERSION_LONG} Source Distribution* from the https://access.redhat.com/jbossnetwork/restricted/listSoftware.html[Red Hat Customer Portal].
endif::[]

In Conway's Game of Life, a user interacts with the game by creating an initial configuration or an advanced pattern with defined properties and then observing how the initial state evolves. The objective of the game is to show the development of a population, generation by generation. Each generation results from the preceding one, based on the simultaneous evaluation of all cells.

The following basic rules govern what the next generation looks like:

* If a live cell has fewer than two live neighbors, it dies of loneliness.
* If a live cell has more than three live neighbors, it dies from overcrowding.
* If a dead cell has exactly three live neighbors, it comes to life.

Any cell that does not meet any of those criteria is left as is for the next generation.

The Conway's Game of Life example uses {PRODUCT} rules with `ruleflow-group` attributes to define the pattern implemented in the game. The example also contains a version of the decision set that achieves the same behavior using agenda groups. Agenda groups enable you to partition the {DECISION_ENGINE} agenda to provide execution control over groups of rules. By default, all rules are in the agenda group `MAIN`. You can use the `agenda-group` attribute to specify a different agenda group for the rule.

This overview does not explore the version of the Conway example using agenda groups. For more information about agenda groups, see the {PRODUCT} example decision sets that specifically address agenda groups.

[discrete]
== Conway example execution and interaction

Similar to other {PRODUCT} decision examples, you execute the Conway ruleflow example by running the `org.drools.examples.conway.ConwayRuleFlowGroupRun` class as a Java application in your IDE.

When you execute the Conway example, the `Conway's Game of Life` GUI window appears. This window contains an empty grid, or "arena" where the life simulation takes place. Initially the grid is empty because no live cells are in the system yet.

.Conway example GUI after launch
image::Examples/ConwaysGameOfLifeExample/conway1.png[align="center"]

Select a predefined pattern from the *Pattern* drop-down menu and click *Next Generation* to click through each population generation. Each cell is either alive or dead, where live cells contain a green ball. As the population evolves from the initial pattern, cells live or die relative to neighboring cells, according to the rules of the game.

.Generation evolution in Conway example
image::Examples/ConwaysGameOfLifeExample/conway2.png[align="center"]

Neighbors include not only cells to the left, right, top, and bottom but also cells that are connected diagonally, so that each cell has a total of eight neighbors. Exceptions are the corner cells, which have only three neighbors, and the cells along the four borders, with five neighbors each.

You can manually intervene to create or kill cells by clicking the cell.

To run through an evolution automatically from the initial pattern, click *Start*.

[discrete]
== Conway example rules with ruleflow groups

The rules in the `ConwayRuleFlowGroupRun` example use ruleflow groups to control rule execution. A ruleflow group is a group of rules associated by the `ruleflow-group` rule attribute. These rules can only fire when the group is activated. The group itself can only become active when the elaboration of the ruleflow diagram reaches the node representing the group.

The Conway example uses the following ruleflow groups for rules:

* `"register neighbor"`
* `"evaluate"`
* `"calculate"`
* `"reset calculate"`
* `"birth"`
* `"kill"`
* `"kill all"`

All of the `Cell` objects are inserted into the KIE session and the `"register ..."` rules in the ruleflow group `"register neighbor"` are allowed to execute by the ruleflow process. This group of four rules creates `Neighbor` relations between some cell and its northeastern, northern, northwestern, and western neighbors.

This relation is bidirectional and handles the other four directions. Border cells do not require any special treatment. These cells are not paired with neighboring cells where there is not any.

By the time all activations have fired for these rules, all cells are related to all their neighboring cells.

.Rules "register ..."
[source]
----
rule "register north east"
    ruleflow-group "register neighbor"
  when
    $cell: Cell( $row : row, $col : col )
    $northEast : Cell( row  == ($row - 1), col == ( $col + 1 ) )
  then
    insert( new Neighbor( $cell, $northEast ) );
    insert( new Neighbor( $northEast, $cell ) );
end

rule "register north"
    ruleflow-group "register neighbor"
  when
    $cell: Cell( $row : row, $col : col )
    $north : Cell( row  == ($row - 1), col == $col )
  then
    insert( new Neighbor( $cell, $north ) );
    insert( new Neighbor( $north, $cell ) );
end

rule "register north west"
    ruleflow-group "register neighbor"
  when
    $cell: Cell( $row : row, $col : col )
    $northWest : Cell( row  == ($row - 1), col == ( $col - 1 ) )
  then
    insert( new Neighbor( $cell, $northWest ) );
    insert( new Neighbor( $northWest, $cell ) );
end

rule "register west"
    ruleflow-group "register neighbor"
  when
    $cell: Cell( $row : row, $col : col )
    $west : Cell( row  == $row, col == ( $col - 1 ) )
  then
    insert( new Neighbor( $cell, $west ) );
    insert( new Neighbor( $west, $cell ) );
end
----

After all the cells are inserted, some Java code applies the pattern to the grid, setting certain cells to `Live`. Then, when the user clicks *Start* or *Next Generation*, the example executes the `Generation` ruleflow. This ruleflow manages all changes of cells in each generation cycle.

.Generation ruleflow
image::Examples/ConwaysGameOfLifeExample/conway_ruleflow_generation.png[align="center"]

The ruleflow process enters the `"evaluate"` ruleflow group and any active rules in the group can fire. The rules `"Kill the ..."` and `"Give Birth"` in this group apply the game rules to birth or kill cells. The example uses the `phase` attribute to drive the reasoning of the `Cell` object by specific groups of rules. Typically, the phase is tied to a ruleflow group in the ruleflow process definition.

Notice that the example does not change the state of any `Cell` objects at this point because it must complete the full evaluation before those changes can be applied. The example sets the cell to a `phase` that is either `Phase.KILL` or `Phase.BIRTH`, which is used later to control actions applied to the `Cell` object.

.Rules "Kill the ..." and "Give Birth"
[source]
----
rule "Kill The Lonely"
    ruleflow-group "evaluate"
    no-loop
  when
    // A live cell has fewer than 2 live neighbors.
    theCell: Cell( liveNeighbors < 2, cellState == CellState.LIVE,
                   phase == Phase.EVALUATE )
  then
    modify( theCell ){
        setPhase( Phase.KILL );
    }
end

rule "Kill The Overcrowded"
    ruleflow-group "evaluate"
    no-loop
  when
    // A live cell has more than 3 live neighbors.
    theCell: Cell( liveNeighbors > 3, cellState == CellState.LIVE,
                   phase == Phase.EVALUATE )
  then
    modify( theCell ){
        setPhase( Phase.KILL );
    }
end

rule "Give Birth"
    ruleflow-group "evaluate"
    no-loop
  when
    // A dead cell has 3 live neighbors.
    theCell: Cell( liveNeighbors == 3, cellState == CellState.DEAD,
                   phase == Phase.EVALUATE )
  then
    modify( theCell ){
        theCell.setPhase( Phase.BIRTH );
    }
end
----

After all `Cell` objects in the grid have been evaluated, the example uses the `"reset calculate"` rule to clear any activations in the `"calculate"` ruleflow group. The example then enters a split in the ruleflow that enables the rules `"kill"` and `"birth"` to fire, if the ruleflow group is activated. These rules apply the state change.

.Rules "reset calculate", "kill", and "birth"
[source]
----
rule "reset calculate"
    ruleflow-group "reset calculate"
  when
  then
    WorkingMemory wm = drools.getWorkingMemory();
    wm.clearRuleFlowGroup( "calculate" );
end

rule "kill"
    ruleflow-group "kill"
    no-loop
  when
    theCell: Cell( phase == Phase.KILL )
  then
    modify( theCell ){
        setCellState( CellState.DEAD ),
        setPhase( Phase.DONE );
    }
end

rule "birth"
    ruleflow-group "birth"
    no-loop
  when
    theCell: Cell( phase == Phase.BIRTH )
  then
    modify( theCell ){
        setCellState( CellState.LIVE ),
        setPhase( Phase.DONE );
    }
end
----

At this stage, several `Cell` objects have been modified with the state changed to either `LIVE` or `DEAD`. When a cell becomes live or dead, the example uses the `Neighbor` relation in the rules `"Calculate ..."` to iterate over all surrounding cells, increasing or decreasing the `liveNeighbor` count. Any cell that has its count changed is also set to the `EVALUATE` phase to make sure it is included in the reasoning during the evaluation stage of the ruleflow process.

After the live count has been determined and set for all cells, the ruleflow process ends. If the user initially clicked *Start*, the {DECISION_ENGINE} restarts the ruleflow at that point. If the user initially clicked *Next Generation*, the user can request another generation.

.Rules "Calculate ..."
[source]
----
rule "Calculate Live"
    ruleflow-group "calculate"
    lock-on-active
  when
    theCell: Cell( cellState == CellState.LIVE )
    Neighbor( cell == theCell, $neighbor : neighbor )
  then
    modify( $neighbor ){
        setLiveNeighbors( $neighbor.getLiveNeighbors() + 1 ),
        setPhase( Phase.EVALUATE );
    }
end

rule "Calculate Dead"
    ruleflow-group "calculate"
    lock-on-active
  when
    theCell: Cell( cellState == CellState.DEAD )
    Neighbor( cell == theCell, $neighbor : neighbor )
  then
    modify( $neighbor ){
        setLiveNeighbors( $neighbor.getLiveNeighbors() - 1 ),
        setPhase( Phase.EVALUATE );
    }
end
----
