= Execution Control

== Agenda


The Agenda is a _Rete_ feature.
It maintains set of rules that are able to execute, its job is to schedule that execution in a deterministic order.

During actions on the ``RuleRuntime``, rules may become fully matched and eligible for execution; a single Rule Runtime Action can result in multiple eligible rules.
When a rule is fully matched a Rule Match is created, referencing the rule and the matched facts, and placed onto the Agenda.
The Agenda controls the execution order of these Matches using a Conflict Resolution strategy.

The {DECISION_ENGINE} cycles repeatedly through two phases:

. Rule Runtime Actions. This is where most of the work takes place, either in the Consequence (the RHS itself) or the main Java application process. Once the Consequence has finished or the main Java application process calls `fireAllRules()` the {DECISION_ENGINE} switches to the Agenda Evaluation phase.
. Agenda Evaluation. This attempts to select a rule to fire. If no rule is found it exits, otherwise it fires the found rule, switching the phase back to Rule Runtime Actions.


.Two Phase Execution
image::UserGuide/Two_Phase.png[]


The process repeats until the agenda is clear, in which case control returns to the calling application.
When Rule Runtime Actions are taking place, no rules are being fired.

== Rule Matches and Conflict Sets.

=== Cashflow Example


So far the data and the matching process has been simple and small.
To mix things up a bit a new example will be explored that handles cashflow calculations over date periods.
The state of the {DECISION_ENGINE} will be illustratively shown at key stages to help get a better understanding of what is actually going on under the hood.
Three classes will be used, as shown below.
This will help us grow our understanding of pattern matching and joins further.
We will then use this to illustrate different techniques for execution control.

[source,java]
----
public class CashFlow {
    private Date   date;
    private double amount;
    private int    type;
    long           accountNo;
    // getter and setter methods here
}

public class Account {
    private long   accountNo;
    private double balance;
    // getter and setter methods here
}

public AccountPeriod {
    private Date start;
    private Date end;
    // getter and setter methods here
}
----


By now you already know how to create KieBases and how to instantiate facts to populate the ``KieSession``, so tables will be used to show the state of the inserted data, as it makes things clearer for illustration purposes.
The tables below show that a single fact was inserted for the ``Account``.
Also inserted are a series of debits and credits as `CashFlow` objects for that account, extending over two quarters.

.CashFlows and Account
image::UserGuide/tables1.png[]

Two rules can be used to determine the debit and credit for that quarter and update the Account balance.
The two rules below constrain the cashflows for an account for a given time period.
Notice the "&&" which use short cut syntax to avoid repeating the field name twice.

[cols="1a,1a", frame="void"]
|===
|[source]
----
rule "increase balance for credits"
when
  ap : AccountPeriod()
  acc : Account( $accountNo : accountNo )
  CashFlow( type == CREDIT,
            accountNo == $accountNo,
            date >= ap.start && <= ap.end,
            $amount : amount )
then
  acc.balance  += $amount;
end
----
|[source]
----
rule "decrease balance for debits" 
when 
  ap : AccountPeriod() 
  acc : Account( $accountNo : accountNo ) 
  CashFlow( type == DEBIT, 
            accountNo == $accountNo,
            date >= ap.start && <= ap.end, 
            $amount : amount ) 
then 
  acc.balance -= $amount; 
end
----
|===


Earlier we showed how rules would equate to SQL, which can often help people with an SQL background to understand rules.
The two rules above can be represented with two views and a trigger for each view, as below:

[cols="1a,1a"]
|===
|[source,sql]
----
select * from Account acc,
              Cashflow cf,
              AccountPeriod ap      
where acc.accountNo == cf.accountNo and 
      cf.type == CREDIT and
      cf.date >= ap.start and 
      cf.date <= ap.end
----
|[source,sql]
----
select * from Account acc, 
              Cashflow cf,
              AccountPeriod ap 
where acc.accountNo == cf.accountNo and 
      cf.type == DEBIT and
      cf.date >= ap.start and 
      cf.date <= ap.end
----
|[source]
----
trigger : acc.balance += cf.amount
----
|[source]
----
trigger : acc.balance -= cf.amount
----
|===

If the `AccountPeriod` is set to the first quarter we constrain the rule "increase balance for credits" to fire on two rows of data and "decrease balance for debits" to act on one row of data.

.AccountingPeriod, CashFlows and Account
image::UserGuide/tables2.png[]

The two cashflow tables above represent the matched data for the two rules.
The data is matched during the insertion stage and, as you discovered in the previous chapter, does not fire straight away, but only after `fireAllRules()` is called.
Meanwhile, the rule plus its matched data is placed on the Agenda and referred to as an RuIe Match or Rule Instance.
The Agenda is a table of Rule Matches that are able to fire and have their consequences executed, as soon as fireAllRules() is called.
Rule Matches on the Agenda are referred to as a [term]_conflict set_
 and their execution is determine by a conflict resolution strategy.
Notice that the order of execution so far is considered arbitrary.

.CashFlows and Account
image::UserGuide/tables7.png[]


After all of the above activations are fired, the account has a balance of -25.

.CashFlows and Account
image::UserGuide/tables3.png[]


If the `AccountPeriod` is updated to the second quarter, we have just a single matched row of data, and thus just a single Rule Match on the Agenda.

The firing of that Activation results in a balance of 25.

.CashFlows and Account
image::UserGuide/tables4.png[]


.CashFlows and Account
image::UserGuide/tables5.png[]


=== Conflict Resolution


What if you don't want the order of rule execution to be arbitrary? When there is one or more Rule Match on the Agenda they are said to be in conflict, and a conflict resolution strategy is used to determine the order of execution.
The Drools strategy is very simple and based around a salience value, which assigns a priority to a rule.
Each rule has a default value of 0, the higher the value the higher the priority. 

As a general rule, it is a good idea not to count on rules firing in any particular order, and to author the rules without worrying about a "flow". However when a flow is needed a number of possibilities exist beyond salience: agenda groups, rule flow groups, activation groups and control/semaphore facts. 

As of Drools 6.0 rule definition order in the source file is used to set priority after salience.

=== Salience


To illustrate Salience we add a rule to print the account balance, where we want this rule to be executed after all the debits and credits have been applied for all accounts.
We achieve this by assigning a negative salience to this rule so that it fires after all rules with the default salience 0.

[cols="1a"]
|===
|[source]
----
rule "Print balance for AccountPeriod"
        salience -50
    when
        ap : AccountPeriod()
        acc : Account()        
    then
        System.out.println( acc.accountNo + " : " + acc.balance );    
end
----
|===


The table below depicts the resulting Agenda.
The three debit and credit rules are shown to be in arbitrary order, while the print rule is ranked last, to execute afterwards.

.CashFlows and Account
image::UserGuide/tables6.png[]


=== Agenda Groups


Agenda groups allow you to place rules into groups, and to place those groups onto a stack.
The stack has push/pop bevaviour.
Calling "setFocus" places the group onto the stack:

[source,java]
----
ksession.getAgenda().getAgendaGroup( "Group A" ).setFocus();
----


The agenda always evaluates the top of the stack.
When all the rules have fired for a group, it is popped from the stack and the next group is evaluated.

[cols="1a,1a"]
|===
|[source,java]
----
rule "increase balance for credits"
  agenda-group "calculation"
when
  ap : AccountPeriod()
  acc : Account( $accountNo : accountNo )
  CashFlow( type == CREDIT,
            accountNo == $accountNo,
            date >= ap.start && <= ap.end,
            $amount : amount )
then
  acc.balance  += $amount;
end
----
|[source,java]
----
rule "Print balance for AccountPeriod"
  agenda-group "report"
when
  ap : AccountPeriod()
  acc : Account()
then
  System.out.println( acc.accountNo +
                      " : " + acc.balance );    
end
----
|===


First set the focus to the "report" group and then by placing the focus on "calculation" we ensure that group is evaluated first.

[source,java]
----
Agenda agenda = ksession.getAgenda();
agenda.getAgendaGroup( "report" ).setFocus();
agenda.getAgendaGroup( "calculation" ).setFocus();
ksession.fireAllRules();
----

=== Rule Flow


Drools also features ruleflow-group attributes which allows workflow diagrams to declaratively specify when rules are allowed to fire.
The screenshot below is taken from Eclipse using the Drools plugin.
It has two ruleflow-group nodes which ensures that the calculation rules are executed before the reporting rules.


image::UserGuide/ruleflow.png[]

The use of the ruleflow-group attribute in a rule is shown below.

[cols="1a,1a"]
|===
|[source]
----
rule "increase balance for credits"
  ruleflow-group "calculation"
when
  ap : AccountPeriod()
  acc : Account( $accountNo : accountNo )
  CashFlow( type == CREDIT,
            accountNo == $accountNo,
            date >= ap.start && <= ap.end,
            $amount : amount )
then
  acc.balance  += $amount;
end
----
|[source]
----
rule "Print balance for AccountPeriod"
  ruleflow-group "report"
when
  ap : AccountPeriod()
  acc : Account()
then
  System.out.println( acc.accountNo +
                      " : " + acc.balance );    
end
----
|===

